plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.11'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.scotiabank'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	//Dependencias para Swagger/OpenAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.3.0'
	//Dependencias para utilizar mapstruct
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	//Dependencias para R2DBC con H2
	implementation 'io.r2dbc:r2dbc-h2'
	runtimeOnly 'com.h2database:h2'
	//Dependencias para Redis reactivo
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	//Dependencias para lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
	
	// Configuración para evitar problemas de bloqueo de archivos en Windows
	outputs.upToDateWhen { false }
	testLogging {
		events "passed", "skipped", "failed"
		exceptionFormat "full"
	}
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/ChallengueApplication.class',
					'**/config/**',
					'**/dto/**',
					'**/enums/**',
					'**/constants/**',
					'**/exception/**',
					'**/util/**'
			])
		}))
	}

}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		rule {
			limit {
				minimum = 0.80
			}
		}
	}
}

tasks.register('openJacocoReport') {
	dependsOn jacocoTestReport
	doLast {
		def reportFile = file("$buildDir/reports/jacoco/test/html/index.html")
		if (reportFile.exists()) {
			exec {
				commandLine 'cmd', '/c', 'start', reportFile.absolutePath
			}
		} else {
			println "No se encontró el reporte de JaCoCo: ${reportFile.absolutePath}"
		}
	}
}

tasks.named('test') {
	finalizedBy 'openJacocoReport'
}

check {
	dependsOn jacocoTestCoverageVerification
}

compileJava {
	options.compilerArgs += ['--enable-preview']
}

test {
	jvmArgs '--enable-preview'
}